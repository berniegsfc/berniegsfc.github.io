<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.6.4 on Fri Aug  1 08:37:21 2025 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>spdfssc__define.pro (Complete SSC R2.4.14 Web Services API)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="spdfssc__define.pro (Complete SSC R2.4.14 Web Services API)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">;</span>
<span class="comments">; NOSA HEADER START</span>
<span class="comments">;</span>
<span class="comments">; The contents of this file are subject to the terms of the NASA Open </span>
<span class="comments">; Source Agreement (NOSA), Version 1.3 only (the "Agreement").  You may </span>
<span class="comments">; not use this file except in compliance with the Agreement.</span>
<span class="comments">;</span>
<span class="comments">; You can obtain a copy of the agreement at</span>
<span class="comments">;   docs/NASA_Open_Source_Agreement_1.3.txt</span>
<span class="comments">; or </span>
<span class="comments">;   https://sscweb.gsfc.nasa.gov/WebServices/NASA_Open_Source_Agreement_1.3.txt.</span>
<span class="comments">;</span>
<span class="comments">; See the Agreement for the specific language governing permissions</span>
<span class="comments">; and limitations under the Agreement.</span>
<span class="comments">;</span>
<span class="comments">; When distributing Covered Code, include this NOSA HEADER in each</span>
<span class="comments">; file and include the Agreement file at </span>
<span class="comments">; docs/NASA_Open_Source_Agreement_1.3.txt.  If applicable, add the </span>
<span class="comments">; following below this NOSA HEADER, with the fields enclosed by </span>
<span class="comments">; brackets "[]" replaced with your own identifying information: </span>
<span class="comments">; Portions Copyright [yyyy] [name of copyright owner}</span>
<span class="comments">;</span>
<span class="comments">; NOSA HEADER END</span>
<span class="comments">;</span>
<span class="comments">; Copyright (c) 2013-2021 United States Government as represented by </span>
<span class="comments">; the National Aeronautics and Space Administration. No copyright is </span>
<span class="comments">; claimed in the United States under Title 17, U.S.Code. All Other </span>
<span class="comments">; Rights Reserved.</span>
<span class="comments">;</span>
<span class="comments">;</span>


<span class="comments">;+</span>
<span class="comments">; This class represents the remotely callable interface to </span>
<span class="comments">; &lt;a href="https://www.nasa.gov/">NASA&lt;/a>'s</span>
<span class="comments">; &lt;a href="https://spdf.gsfc.nasa.gov/">Space Physics Data Facility&lt;/a> </span>
<span class="comments">; (SPDF)</span>
<span class="comments">; &lt;a href="https://sscweb.gsfc.nasa.gov/">Satellite Situation Center&lt;/a></span>
<span class="comments">; (SSC).  The current implementation only support the </span>
<span class="comments">; &lt;a href="https://sscweb.gsfc.nasa.gov/WebServices/REST/#Get_Locations_POST"></span>
<span class="comments">; "data locations" functionality&lt;/a>.  Supporting the "text (listing)</span>
<span class="comments">; locations", "conjunctions" and/or "graphs" functionality is a </span>
<span class="comments">; future possibility if there were sufficient interest.</span>
<span class="comments">;</span>
<span class="comments">; @copyright Copyright (c) 2013-2017 United States Government as </span>
<span class="comments">;     represented by the National Aeronautics and Space Administration.</span>
<span class="comments">;     No copyright is claimed in the United States under Title 17, </span>
<span class="comments">;     U.S.Code. All Other Rights Reserved.</span>
<span class="comments">;</span>
<span class="comments">; @author B. Harris</span>
<span class="comments">;-</span>


<span class="comments">;+</span>
<span class="comments">; Creates an object representing SSC.</span>
<span class="comments">;</span>
<span class="comments">; @keyword endpoint {in} {optional} {type=string}</span>
<span class="comments">;              {default=self->getDefaultEndpoint()}</span>
<span class="comments">;              URL of SSC web service .</span>
<span class="comments">; @keyword userAgent {in} {optional} {type=string} {default=SscWs}</span>
<span class="comments">;              HTTP user-agent value used in communications with SSC.</span>
<span class="comments">; @keyword sslVerifyPeer {in} {optional} {type=int} {default=1}</span>
<span class="comments">;              Specifies whether the authenticity of the peer's SSL</span>
<span class="comments">;              certificate should be verified.  When 0, the connection</span>
<span class="comments">;              succeeds regardless of what the peer SSL certificate</span>
<span class="comments">;              contains.</span>
<span class="comments">; @returns a reference to a SSC object.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::init:source"></a>function SpdfSsc::init, $
    endpoint = endpoint, $
    userAgent = userAgent, $
    sslVerifyPeer = sslVerifyPeer
    compile_opt idl2

    version = '%VERSION*'
    currentVersionUrl = $
        'https://sscweb.gsfc.nasa.gov/WebServices/REST/spdfSscVersion.txt'

    if ~keyword_set(endpoint) then begin

        endpoint = self->getDefaultEndpoint()
    endif

    if ~keyword_set(userAgent) then userAgent = 'SscWs'

    obj = self->SpdfRest::init( $
              endpoint, version, currentVersionUrl, $
              userAgent = userAgent + '/' + version, $
              sslVerifyPeer = sslVerifyPeer)

    return, obj
end


<span class="comments">;+</span>
<span class="comments">; Performs cleanup operations when this object is destroyed.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::cleanup:source"></a>pro SpdfSsc::cleanup
    compile_opt idl2

    self->SpdfRest::cleanup
end


<span class="comments">;+</span>
<span class="comments">; Gets the default endpoint value.</span>
<span class="comments">;</span>
<span class="comments">; @returns default endpoint string value.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getDefaultEndpoint:source"></a>function SpdfSsc::getDefaultEndpoint
    compile_opt idl2

    endpoint = 'http'

    releaseComponents = strsplit(!version.release, '.', /extract)

    if (releaseComponents[0] ge '8') or $
       (releaseComponents[0] eq '8' and $
        releaseComponents[1] ge '4') then begin

        <span class="comments">; Even though earlier versions of IDL are suppose to support</span>
        <span class="comments">; https, they do not (at least they do not support https to</span>
        <span class="comments">; cdaweb).</span>

        endpoint = endpoint + 's'
    endif

    return, endpoint + '://sscweb.gsfc.nasa.gov/WS/sscr/2'
end


<span class="comments">;+</span>
<span class="comments">; Gets a description of all the observatories that are available.</span>
<span class="comments">;</span>
<span class="comments">; @keyword httpErrorReporter {in} {optional} </span>
<span class="comments">;              {type=SpdfHttpErrorReporter}</span>
<span class="comments">;              used to report an HTTP error.</span>
<span class="comments">; @returns array of SpdfObservatoryDescription objects.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getObservatories:source"></a>function SpdfSsc::getObservatories, $
    httpErrorReporter=errorReporter
    compile_opt idl2

    url = self.endpoint + '/observatories'

    observatoryDom = $
        self->makeGetRequest(url, errorReporter=errorReporter)

    if ~obj_valid(observatoryDom) then return, objarr(1)

    observatoryElements = $
        observatoryDom->getElementsByTagName('Observatory')

    observatories = objarr(observatoryElements->getLength(), /nozero)

    for i = 0, observatoryElements->getLength() - 1 do begin

        observatoryElement = observatoryElements->item(i)

        id = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'Id')
        name = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'Name')
        resolution = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'Resolution')
        startTime = $
            self->getJulDate((observatoryElement->$
                getElementsByTagName('StartTime'))->item(0))
        endTime = $
            self->getJulDate((observatoryElement->$
                getElementsByTagName('EndTime'))->item(0))
        geometry = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'Geometry')
        trajectoryGeometry = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'TrajectoryGeometry')
        resourceId = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'ResourceId')
        groupIds = $
            self->getNamedElementsFirstChildValue( $
                observatoryElement, 'GroupId')

        observatories[i] = $
            obj_new('SpdfObservatoryDescription', id, name, $
                resolution, startTime, endTime, geometry, $
                trajectoryGeometry, resourceId, groupIds)
    endfor

    obj_destroy, observatoryDom

    return, observatories
end
    

<span class="comments">;+</span>
<span class="comments">; Gets a description of all the ground stations that are available.</span>
<span class="comments">;</span>
<span class="comments">; @keyword httpErrorReporter {in} {optional} </span>
<span class="comments">;              {type=SpdfHttpErrorReporter}</span>
<span class="comments">;              used to report an HTTP error.</span>
<span class="comments">; @returns array of SpdfGroundStation objects.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getGroundStations:source"></a>function SpdfSsc::getGroundStations, $
    httpErrorReporter=errorReporter
    compile_opt idl2

    url = self.endpoint + '/groundStations'

    gsDom = self->makeGetRequest(url, errorReporter=errorReporter)

    if ~obj_valid(gsDom) then return, objarr(1)

    gsElements = gsDom->getElementsByTagName('GroundStation')

    groundStations = objarr(gsElements->getLength(), /nozero)

    for i = 0, gsElements->getLength() - 1 do begin

        gsElement = gsElements->item(i)

        id = $
            self->getNamedElementsFirstChildValue(gsElement, 'Id')
        name = $
            self->getNamedElementsFirstChildValue(gsElement, 'Name')
        latitude = $
            self->getNamedElementsFirstChildValue( $
                gsElement, 'Latitude')
        longitude = $
            self->getNamedElementsFirstChildValue( $
                gsElement, 'Longitude')

        location = obj_new('SpdfSurfaceGeographicCoordinates', $
                       latitude, longitude)

        groundStations[i] = $
            obj_new('SpdfGroundStation', id, name, location)
    endfor

    obj_destroy, gsDom

    return, groundStations
end


<span class="comments">;+</span>
<span class="comments">; Request the specified satellite location information from SSC.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param locationRequest {in} {type=SpdfLocationRequest}</span>
<span class="comments">;            specifies the information to get.</span>
<span class="comments">; @keyword httpErrorReporter {in} {optional} </span>
<span class="comments">;              {type=SpdfHttpErrorReporter}</span>
<span class="comments">;              used to report an HTTP error.</span>
<span class="comments">; @returns an SpdfSscDataResult or SpdfSscFileResult object.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getLocations:source"></a>function SpdfSsc::getLocations, $
    locationRequest, $
    httpErrorReporter = errorReporter
    compile_opt idl2

    url = self.endpoint + '/locations'

    requestDoc = obj_new('IDLffXMLDOMDocument')

    requestDom = $
        requestDoc->appendChild( $
            locationRequest->createDomElement(requestDoc))

    requestDoc->save, string=xmlRequest

    obj_destroy, requestDoc

    resultDoc = self->makePostRequest(url, xmlRequest, $
                    errorReporter = errorReporter)

    if ~obj_valid(resultDoc) then return, obj_new()

    fileNodeList= resultDoc->getElementsByTagName('Files')

    if fileNodeList->getLength() eq 0 then begin

        dataResult = self->getDataResult(resultDoc)
    endif else begin

        dataResult = self->getFileResult(resultDoc)
    endelse

    obj_destroy, resultDoc

    return, dataResult
end


<span class="comments">;+</span>
<span class="comments">; Gets the node's time values from the given DOM element.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param domElement {in} {required} {type=IDLffXMLDOMElement}</span>
<span class="comments">;                DOM element to search.</span>
<span class="comments">; @keyword name {in} {optional} {type=string} {default='Time'}</span>
<span class="comments">;              name of time tag.</span>
<span class="comments">; @returns node's julday time value(s) from the given DOM element.</span>
<span class="comments">;     A scalar value of !values.d_NaN is returned if no value is found.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getTime:source"></a>function SpdfSsc::getTime, $
    domElement, $
    name = name
    compile_opt idl2

    if keyword_set(name) then name = name else name = 'Time'

    nodeList = domElement->getElementsByTagName(name)

    if nodeList->getLength() eq 0 then return, !values.d_NaN

    values = dblarr(nodeList->getLength())

    for i = 0, nodeList->getLength() - 1 do begin

        domNode = nodeList->item(i)

        values[i] = self->getJulDate(domNode)
    endfor

    if n_elements(values) eq 1 then return, values[0] $
                               else return, values
end


<span class="comments">;+</span>
<span class="comments">; Gets the specified node values from the given ssc:DataResult XML</span>
<span class="comments">; document.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param resultDoc {in} {type=IDLffXMLDOMDocument}</span>
<span class="comments">;            SpdfSscDataResult XML document.</span>
<span class="comments">; @param type {in} {type=string}</span>
<span class="comments">;            name of elements whose value is to be gotten.</span>
<span class="comments">; @returns strarr of the specified node values from the given document.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getDataResultText:source"></a>function SpdfSsc::getDataResultText, $
    resultDoc, type
    compile_opt idl2

    return, self->getNamedElementsFirstChildValue(resultDoc, type)
end


<span class="comments">;+</span>
<span class="comments">; Creates an SpdfSscFileResult object from the given ssc:DataResult</span>
<span class="comments">; XML document.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param doc {in} {type=IDLffXMLDOMDocument}</span>
<span class="comments">;              SpdfSscDataResult XML document.</span>
<span class="comments">; @returns SpdfSscFileResult object.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getFileResult:source"></a>function SpdfSsc::getFileResult, $
    doc
    compile_opt idl2

<span class="comments">; doc->save, filename='getFileResult.xml', /pretty_print</span>

    statusCode = self->getDataResultText(doc, 'StatusCode')

    statusSubCode = self->getDataResultText(doc, 'StatusSubCode')

    statusText = self->getDataResultText(doc, 'StatusText')

    if statusCode eq '' then begin

        <span class="comments">; This will not be necessary when the server is fixed to</span>
        <span class="comments">; include it in a FileResult (fix is in R2.2.7).</span>
        statusCode = 'Success'
    endif
    if statusCode ne 'Success' then begin

        print, 'StatusCode: ', statusCode
        print, 'StatusSubCode: ', statusSubCode
        print, 'StatusText: ', statusText

        return, obj_new()
    endif

    files = self->getFiles(doc)

    fileResult = obj_new('SpdfSscFileResult', $
        files, $
        statusCode = statusCode, $
        statusSubCode = statusSubCode, $
        statusText = statusText)

    return, fileResult
end


<span class="comments">;+</span>
<span class="comments">; Creates an array of SpdfSscFileDescription objects from the given </span>
<span class="comments">; ssc:Files XML document.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param doc {in} {type=IDLffXMLDOMDocument}</span>
<span class="comments">;              SpdfFileResult XML document.</span>
<span class="comments">; @returns objarr containing SpdfSscFileDescriptions or an objarr(1) whose</span>
<span class="comments">;     first element is ~obj_valid().</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getFiles:source"></a>function SpdfSsc::getFiles, $
    doc
    compile_opt idl2

<span class="comments">; doc->save, filename='files.xml', /pretty_print</span>

    filesElements = $
        doc->getElementsByTagName('Files')

    if filesElements->getLength() eq 0 then begin

        return, objarr(1)
    endif

    files = objarr(filesElements->getLength())

    for i = 0, filesElements->getLength() - 1 do begin

        filesElement = filesElements->item(i)

        name = self->getNamedElementsFirstChildValue( $
                   filesElement, 'Name')

        mimeType = self->getNamedElementsFirstChildValue( $
                       filesElement, 'MimeType')

        length = self->getNamedElementsFirstChildValue( $
                   filesElement, 'Length')

        lastModified = self->getTime(filesElement, name='LastModified')

        files[i] = obj_new('SpdfSscFileDescription', $
            name, $
            mimeType, $
            length, $
            lastModified)
    endfor

    return, files
end


<span class="comments">;+</span>
<span class="comments">; Creates an SpdfSscDataResult object from the given ssc:DataResult</span>
<span class="comments">; XML document.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param doc {in} {type=IDLffXMLDOMDocument}</span>
<span class="comments">;              SpdfSscDataResult XML document.</span>
<span class="comments">; @returns SpdfSscDataResult object.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getDataResult:source"></a>function SpdfSsc::getDataResult, $
    doc
    compile_opt idl2

doc->save, filename='getDataResult.xml', /pretty_print

    statusCode = self->getDataResultText(doc, 'StatusCode')

    statusSubCode = self->getDataResultText(doc, 'StatusSubCode')

    statusText = self->getDataResultText(doc, 'StatusText')

    if statusCode ne 'Success' then begin

        print, 'StatusCode: ', statusCode
        print, 'StatusSubCode: ', statusSubCode
        print, 'StatusText: ', statusText

        return, obj_new()
    endif

    data = self->getSatelliteData(doc)

    dataResult = obj_new('SpdfSscDataResult', $
        data, $
        statusCode = statusCode, $
        statusSubCode = statusSubCode, $
        statusText = statusText)

    return, dataResult
end


<span class="comments">;+</span>
<span class="comments">; Creates an SpdfSatelliteData object from the given ssc:SatelliteData</span>
<span class="comments">; XML document.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param doc {in} {type=IDLffXMLDOMDocument}</span>
<span class="comments">;              SpdfSatelliteData XML document.</span>
<span class="comments">; @returns objarr containing SpdfSatelliteData or an objarr(1) whose</span>
<span class="comments">;     first element is ~obj_valid().</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getSatelliteData:source"></a>function SpdfSsc::getSatelliteData, $
    doc
    compile_opt idl2

<span class="comments">; doc->save, filename='satData.xml', /pretty_print</span>

    satDataElements = $
        doc->getElementsByTagName('Data')

    if satDataElements->getLength() eq 0 then begin

        return, objarr(1)
    endif

    satData = objarr(satDataElements->getLength())

    for i = 0, satDataElements->getLength() - 1 do begin

        satDataElement = satDataElements->item(i)

        id = $
            self->getNamedElementsFirstChildValue( $
                satDataElement, 'Id')

        coordinateData = $
            self->getCoordinateData(satDataElement)

        time = self->getTime(satDataElement)

        bTraceData = self->getBTraceData(satDataElement)

        radialLength = $
            self->getDoubleSatelliteData( $
                satDataElement, 'RadialLength')

        magneticStrength = $
            self->getDoubleSatelliteData( $
                satDataElement, 'MagneticStrength')

        neutralSheetDistance = $
            self->getDoubleSatelliteData( $
                satDataElement, 'NeutralSheetDistance')

        bowShockDistance = $
            self->getDoubleSatelliteData( $
                satDataElement, 'BowShockDistance')

        magnetoPauseDistance = $
            self->getDoubleSatelliteData( $
                satDataElement, 'MagnetoPauseDistance')

        dipoleLValue = $
            self->getDoubleSatelliteData( $
                satDataElement, 'DipoleLValue')

        dipoleInvariantLatitude = $
            self->getFloatSatelliteData( $
                satDataElement, 'DipoleInvariantLatitude')

        spacecraftRegion = $
            self->getSatelliteRegionData( $
                satDataElement, 'SpacecraftRegion')

        radialTracedFootpointRegions = $
            self->getSatelliteRegionData( $
                satDataElement, 'RadialTracedFootpointRegions')

        bGseX = $
            self->getDoubleSatelliteData(satDataElement, 'BGseX')

        bGseY = $
            self->getDoubleSatelliteData(satDataElement, 'BGseY')

        bGseZ = $
            self->getDoubleSatelliteData(satDataElement, 'BGseZ')

        northBTracedFootpointRegions = $
            self->getSatelliteRegionData( $
                satDataElement, 'NorthBTracedFootpointRegions')

        southBTracedFootpointRegions = $
            self->getSatelliteRegionData( $
                satDataElement, 'SouthBTracedFootpointRegions')

        satData[i] = obj_new('SpdfSatelliteData', $
            id, $
            coordinateData, $
            time, $
            bTraceData = bTraceData, $
            radialLength = radialLength, $
            magneticStrength = magneticStrength, $
            neutralSheetDistance = neutralSheetDistance, $
            bowShockDistance = bowShockDistance, $
            magnetoPauseDistance = magnetoPauseDistance, $
            dipoleLValue = dipoleLValue, $
            dipoleInvariantLatitude = dipoleInvariantLatitude, $
            spacecraftRegion = spacecraftRegion, $
            radialTracedFootpointRegions = $
                radialTracedFootpointRegions, $
            bGseX = bGseX, $
            bGseY = bGseY, $
            bGseZ = bGseZ, $
            northBTracedFootpointRegions = $
                northBTracedFootpointRegions, $
            southBTracedFootpointRegions = $
                southBTracedFootpointRegions)
    endfor

    return, satData
end


<span class="comments">;+</span>
<span class="comments">; Creates an SpdfCoordinateData object from the given ssc:SatelliteData</span>
<span class="comments">; XML element.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param satDataElement {in} {type=IDLffXMLDOMElement}</span>
<span class="comments">;              SpdfSatelliteData XML element.</span>
<span class="comments">; @returns SpdfCoordinateData object.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getCoordinateData:source"></a>function SpdfSsc::getCoordinateData, $
    satDataElement
    compile_opt idl2

    nodeList = satDataElement->getElementsByTagName('Coordinates')

    if nodeList->getLength() eq 0 then return, obj_new()

    coordinatesElement = nodeList->item(0)

    coordinateSystem = $
        self->getNamedElementsFirstChildValue( $
            coordinatesElement, 'CoordinateSystem')

    x = self->getNamedElementsFirstChildDoubleValue( $
            coordinatesElement, 'X')

    y = self->getNamedElementsFirstChildDoubleValue( $
            coordinatesElement, 'Y')

    z = self->getNamedElementsFirstChildDoubleValue( $
            coordinatesElement, 'Z')

    latitude = self->getNamedElementsFirstChildFloatValue( $
            coordinatesElement, 'Latitude')

    longitude = self->getNamedElementsFirstChildFloatValue( $
            coordinatesElement, 'Longitude')

    localTime = self->getNamedElementsFirstChildDoubleValue( $
            coordinatesElement, 'LocalTime')

    coordinateData = $
        obj_new('SpdfCoordinateData', $
            coordinateSystem, $
            x = x, $
            y = y, $
            z = z, $
            latitude = latitude, $
            longitude = longitude, $
            localTime = localTime)

    return, coordinateData
end


<span class="comments">;+</span>
<span class="comments">; Creates an SpdfBTraceData object from the given ssc:BTraceData</span>
<span class="comments">; XML element.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param satDataElement {in} {type=IDLffXMLDOMElement}</span>
<span class="comments">;              SpdfSatelliteData XML element.</span>
<span class="comments">; @returns SpdfCoordinateData object.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getBTraceData:source"></a>function SpdfSsc::getBTraceData, $
    satDataElement
    compile_opt idl2

    bTraceDataElements = $
        satDataElement->getElementsByTagName('BTraceData')

    if bTraceDataElements->getLength() eq 0 then begin

        return, objarr(1)
    endif

    bTraceData = objarr(bTraceDataElements->getLength())

    for i = 0, bTraceDataElements->getLength() - 1 do begin

        bTraceDataElement = bTraceDataElements->item(i)

        coordinateSystem = $
            self->getNamedElementsFirstChildValue( $
                bTraceDataElement, 'CoordinateSystem')

        hemisphere = $
            self->getNamedElementsFirstChildValue( $
                bTraceDataElement, 'Hemisphere')

        latitude = self->getNamedElementsFirstChildFloatValue( $
                bTraceDataElement, 'Latitude')

        longitude = self->getNamedElementsFirstChildFloatValue( $
                bTraceDataElement, 'Longitude')

        arcLength = self->getNamedElementsFirstChildDoubleValue( $
                bTraceDataElement, 'ArcLength')

        bTraceData[i] = $
            obj_new('SpdfBTraceData', $
                coordinateSystem, $
                hemisphere, $
                latitude = latitude, $
                longitude = longitude, $
                arcLength = arcLength)
    endfor

    return, bTraceData
end


<span class="comments">;+</span>
<span class="comments">; Gets the specified double data values from the given satellite data.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param satDataElement {in} {type=IDLffXMLDOMElement}</span>
<span class="comments">;              Satellite XML element.</span>
<span class="comments">; @param name {in} {type=string}</span>
<span class="comments">;            name of satellite data to get.</span>
<span class="comments">; @returns array of double values.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getDoubleSatelliteData:source"></a>function SpdfSsc::getDoubleSatelliteData, $
    satDataElement, $
    name
    compile_opt idl2

    values = self->getNamedElementsFirstChildDoubleValue( $
            satDataElement, name)

    return, values
end


<span class="comments">;+</span>
<span class="comments">; Gets the specified float data values from the given satellite data.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param satDataElement {in} {type=IDLffXMLDOMElement}</span>
<span class="comments">;              Satellite XML element.</span>
<span class="comments">; @param name {in} {type=string}</span>
<span class="comments">;            name of satellite data to get.</span>
<span class="comments">; @returns array of float values.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getFloatSatelliteData:source"></a>function SpdfSsc::getFloatSatelliteData, $
    satDataElement, $
    name
    compile_opt idl2

    values = self->getNamedElementsFirstChildFloatValue( $
            satDataElement, name)

    return, values
end


<span class="comments">;+</span>
<span class="comments">; Gets the specified region data values from the given satellite data.</span>
<span class="comments">;</span>
<span class="comments">; @private</span>
<span class="comments">;</span>
<span class="comments">; @param satDataElement {in} {type=IDLffXMLDOMElement}</span>
<span class="comments">;              Satellite XML element.</span>
<span class="comments">; @param name {in} {type=string}</span>
<span class="comments">;            name of region data to get.</span>
<span class="comments">; @returns strarr of region values.  An empty string is returned </span>
<span class="comments">;     if the value cannot be found.</span>
<span class="comments">;-</span>
<a id="SpdfSsc::getSatelliteRegionData:source"></a>function SpdfSsc::getSatelliteRegionData, $
    satDataElement, $
    name
    compile_opt idl2

    values = self->getNamedElementsFirstChildValue( $
                satDataElement, name)

    return, values
end


<span class="comments">;+</span>
<span class="comments">; Defines the SpdfSsc class.</span>
<span class="comments">;</span>
<span class="comments">;-</span>
<a id="SpdfSsc__define:source"></a>pro SpdfSsc__define
    compile_opt idl2
    struct = { SpdfSsc, $
        inherits SpdfRest $
    }
end
</code>
    </div>
  </body>
</html>